apply plugin: 'com.android.application'

android {
	compileSdkVersion = COMPILE_SDK_VERSION.toInteger()
	buildToolsVersion = BUILD_TOOLS_VERSION
	defaultConfig {
		applicationId = PACKAGE_NAME
		minSdkVersion = MIN_SDK_VERSION.toInteger()
		targetSdkVersion = TARGET_SDK_VERSION.toInteger()
		versionCode = STORE_VERSION.toInteger()
		versionName = VERSION_DISPLAY_NAME
	}
}

apply from: 'buildAdditions.gradle'

android {
	buildTypes {
		release {
			signingConfig signingConfigs.release
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
		debug {
			debuggable true
		}
	}

	// work in progress
	//externalNativeBuild {
	//	cmake {
	//		path 'CMakeLists.txt'
	//	}
	//}
	
	sourceSets.main {
		// disable generation of Android.mk
		jni.srcDirs = []
		
		jniLibs.srcDirs = ['src/main/libs']
		
		// aidl in java directories
		aidl.srcDirs = java.srcDirs
	}

	lintOptions {
		// uncomment to get a report
		//htmlOutput file("lint-report.html")

		// turn this back on if you want to check
		checkReleaseBuilds false
		
		// this will prevent it from stopping if above is true and an issue is found
		abortOnError false
	}

	applicationVariants.all {
		outputs.each {
			// directly write final APK to Binaries/Android with proper name
			it.outputFile = file(OUTPUT_PATH)
		}
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	compile fileTree(dir: 'src/main/libs', include: ['*.jar'])
}
